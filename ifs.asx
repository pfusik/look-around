	opt	h-
	dta	a($801)
	org	$801
	opt	f+
	dta	a($80b,$f),$9e,c'2059',0,$a0,0

Screen_colors   equ	$0400	$3e8
Screen_screen   equ	$2000	$1f40
Screen_lineLo   equ	$4000
Screen_lineHi   equ	$4100
Screen_mask     equ	$4200
Ifs_branchTable equ	$4300
Ifs_mulLookups  equ	$5000	$1400

Screen_ptr        equ	$80	2
Ifs_ptr           equ	$82	2
Ifs_currentBranch equ	$84
Ifs_pLimit        equ	$85
Ifs_y             equ	$86
Ifs_defIndex      equ	$87
Ifs_step          equ	$88	2
Ifs_signs         equ	$8a
memset_limit      equ	$8b

	lda	#0
;	jsr	$1000
	sei
	mva	#$35	1
	mvx	#$7f	$dc0d
	ldx	$dc0d
	mvx	#0	$dc0e
	inx:stx	$d01a
	stx	$d012
;	mwa	#irq	$fffe

	mwa	#$ffff	$d40e ; high frequency random
	mva	#$80	$d412 ; enable random

	ldx	#0
	lda	#$80
Screen_initMask
	sta	Screen_mask,x
	lsr	@
	scc:ror	@
	inx
	bne	Screen_initMask

	lda	<Screen_screen+4*8
	ldx	>Screen_screen+4*8
	ldy	#0
Screen_initLine1
	sta	Screen_lineLo,y
	txa:sta	Screen_lineHi,y
	tya
	and:cmp	#7
	lda	Screen_lineLo,y
	bcc	Screen_initLine2
	adc	<311	+
	scc:inx
	inx
Screen_initLine2
	iny:cpy	#200
	bcc	Screen_initLine1

	mva	<Ifs_mulLookups	Ifs_ptr

	lda:rne	$d012	; raster
;	cli
	mva	#$3b	$d011	; bitmap ($20), noblank ($10), 25 rows (8), yscroll (3)
	mva	#$00	$d016	; 0*multicolor (0*$10), 38 col (0*8), xscroll(0)
	mva	#$18	$d018	; colors at $400 ($10/$10 * $400), bitmap at $2000 (8 * $400)
 	mva	#0	$d020	; border color

	lda	#$50
	ldx	>Screen_colors
	ldy	>Screen_colors+$400
	jsr	memset
	lda	#0
	ldx	>Screen_screen
	ldy	>Screen_screen+$2000
	jsr	memset

	ldx	#0

	mva	>Ifs_mulLookups	Ifs_ptr+1
	mva	#Ifs_0-Ifs_branch	Ifs_currentBranch
	ldy	#0

Ifs_trans
; odczytaj limit p-twa
	mva	Ifs_def,x+	Ifs_pLimit
; zapisz odgalezienia
	lda	Ifs_currentBranch
Ifs_fillBranchTable
	sta	Ifs_branchTable,y+
	cpy	Ifs_pLimit
	bne	Ifs_fillBranchTable
	sbc	#Ifs_0-Ifs_1	+
	sta	Ifs_currentBranch
; przygotuj tabele mnozenia
	mva	Ifs_def,x+	Ifs_signs
	jsr	Ifs_init2MulLookups
	jsr	Ifs_init2MulLookups
	ldy	Ifs_pLimit
	bne	Ifs_trans

	lda	Ifs_def,x+
	mvy	Ifs_def,x+	Ifs_y
	jmp	Ifs_set

	jmp	*

Ifs_4
	lda	Ifs_mulLookups+$1200,x
	adc	Ifs_mulLookups+$1300,y
	sta	Ifs_y
	clc
	lda	Ifs_mulLookups+$1000,x
	adc	Ifs_mulLookups+$1100,y
	jmp	Ifs_set
Ifs_3
	lda	Ifs_mulLookups+$e00,x
	adc	Ifs_mulLookups+$f00,y
	sta	Ifs_y
	clc
	lda	Ifs_mulLookups+$c00,x
	adc	Ifs_mulLookups+$d00,y
	jmp	Ifs_set
Ifs_2
	lda	Ifs_mulLookups+$a00,x
	adc	Ifs_mulLookups+$b00,y
	sta	Ifs_y
	clc
	lda	Ifs_mulLookups+$800,x
	adc	Ifs_mulLookups+$900,y
	jmp	Ifs_set
Ifs_1
	lda	Ifs_mulLookups+$600,x
	adc	Ifs_mulLookups+$700,y
	sta	Ifs_y
	clc
	lda	Ifs_mulLookups+$400,x
	adc	Ifs_mulLookups+$500,y
	jmp	Ifs_set
Ifs_0
	lda	Ifs_mulLookups+$200,x
	adc	Ifs_mulLookups+$300,y
	sta	Ifs_y
	clc
	lda	Ifs_mulLookups,x
	adc	Ifs_mulLookups+$100,y
Ifs_set
	ldy	$d41b	; random
	ldx	Ifs_branchTable,y
	stx	Ifs_branch-1
	tax
	ldy	Ifs_y
	and	#~7
	add	Screen_lineLo,y
	sta	Screen_ptr
	and	#0
	adc	Screen_lineHi,y
	sta	Screen_ptr+1
	lda	Screen_mask,x
	ora:sta	(Screen_ptr),y
	bcc	*	! .
Ifs_branch

memset
	sty	memset_limit
	ldy	#0
memset_1
	stx	memset_sta+2
memset_sta
	sta:rne	a:0,y+
	inx
	cpx	memset_limit
	bcc	memset_1
	rts

Ifs_init2MulLookups
	ldy	Ifs_def,x
	lda	#0
	jsr	Ifs_initMulLookup
	ldy	Ifs_def,x+
	lda	Ifs_def,x
Ifs_initMulLookup
	inx
	stx	Ifs_defIndex
	sty	Ifs_step
	ldy	#0
	lsr	Ifs_signs
	scc:dey
	sty	Ifs_step+1
	sta	(Ifs_ptr),0
	ldx	#0
Ifs_initMulLookup1
	txa
	adc	Ifs_step
	tax
	lda	Ifs_step+1
	adc	(Ifs_ptr),y+
	sta	(Ifs_ptr),y
	bne	Ifs_initMulLookup1
	inc	Ifs_ptr+1
	ldx	Ifs_defIndex
	rts

Ifs_def
; paprotka Barnsleya
 ift 0
	dta	186,%0010	; p, znaki %dcba
	dta	217,-9-1,37	; a,b,e
	dta	9,217,-5-1	; c,d,f
	dta	186+33,%0100
	dta	50,57,6
	dta	-57-1,50,129
	dta	186+33+28,%1110
	dta	63,-70-1,85
	dta	-64-1,-40-1,223
	dta	0,%0110
	dta	14,-19-1,45
	dta	-19-1,26,176
	dta	114,47 ; x,y punktu stalego
 eli 0
; drzewo
	dta	76,%0100
	dta	49,124,14
	dta	-88,113,63
	dta	76+76,%0010
	dta	118,-105,151
	dta	64,92,-3
	dta	76+76+12,%1101
	dta	-14,17,140
	dta	-115,-28,212
	dta	76+76+12+12,%1011
	dta	-8,-17,143
	dta	120,-5,85
	dta	0,%0001
	dta	-163,0,223
	dta	0,128,44
	dta	63,74
 eli 0
; krysztalek 4
	dta	25,%0000
	dta	65,0,100
	dta	0,65,4
	dta	25+25,%0000
	dta	65,0,30
	dta	0,65,125
	dta	25+25+25,%0000
	dta	65,0,170
	dta	0,65,125
	dta	0,%0100
	dta	94,164,11
	dta	-164,94,159
	dta	135,6
 eli 1
; krysztalek 5
	dta	51,%0000
	dta	97,0,79
	dta	0,97,3
	dta	51+51,%0000
	dta	97,0,142
	dta	0,97,48
	dta	51+51+51,%0000
	dta	97,0,18
	dta	0,97,48
	dta	51+51+51+51,%0000
	dta	97,0,41
	dta	0,97,120
	dta	0,%0000
	dta	97,0,118
	dta	0,97,120
	dta	129,5
 eif
	end
