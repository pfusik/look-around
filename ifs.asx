; Iterated Function System

TEST_OVERFLOW	equ	0

screen	equ	$6000	$1800
mask	equ	$7800
col	equ	$7900
lineLo	equ	$7a00
lineHi	equ	$7b00
mulLookups	equ	$7c00	$1400
branchTable	equ	$9000

ptr	equ	$80	2
y	equ	$82
defsIndex	equ	$83
step	equ	$84	2
signs	equ	$86
currentBranch	equ	$87
pLimit	equ	$88

	org	$9100
dl	dta	$70,$70,$70,$4f,a(screen)
:127	dta	$0f
	dta	$4f,a(screen+$1000)
:63	dta	$0f
	dta	$41,a(dl)

cls
	lda:tay	#0
	ldx	>screen
cls_1
	stx	cls_2+2
cls_2
	sta:rne	screen,y+
	inx:cpx	>screen+$1800
	bcc	cls_1
	rts

init2MulLookups
	ldy	defs,x
	lda	#0
	jsr	initMulLookup
	ldy	defs,x+
	lda	defs,x
initMulLookup
	inx
	stx	defsIndex
	sty	step
	ldy	#0
	lsr	signs
	scc:dey
	sty	step+1
	sta	(ptr),0
	ldx	#0
initMulLookup_1
	txa
	adc	step
	tax
	lda	step+1
	adc	(ptr),y+
	sta	(ptr),y
	bne	initMulLookup_1
	inc	ptr+1
	ldx	defsIndex
	rts

main
	sei:inc	^4e
	jsr	cls
	ldx	#0
	lda	#$80
	ldy	#0
init_1
	sta	mask,x
	tya:sta	col,x
	add	>screen
	sta	lineHi,x
	txa
:5	asl	@
	sta	lineLo,x
	lda	mask,x
	lsr	@
	bcc	init_2
	ror	@
	iny
init_2
	inx
	bne	init_1
	
	lda:rne	^4b
	ldx	#29
	sta:rpl	^00,x-
	sta	ptr
	sta	defsIndex
	mva	#$21	^40
	mwa	#dl	^42
	mva	#$fe	^31
	mwa	#next	$fffe
next
	mva	#0	^2e
	mva	#$40	^2e
	cli

new
	jsr	cls
	ldx	defsIndex
	lda	defs,x
	sne:ldx	#0
	lda:rne	^4b
	mva	defs,x+	^1a
	sta	^18
	mva	defs,x+	^17
	mva	>mulLookups	ptr+1
	mva	#case_0-branch	currentBranch
	ldy	#0
new_1
	mva	defs,x+	pLimit
	lda	currentBranch
new_2
	sta	branchTable,y+
	cpy	pLimit
	bne	new_2
	sbc	#case_0-case_1	+
	sta	currentBranch
	mva	defs,x+	signs
	jsr	init2MulLookups
	jsr	init2MulLookups
	ldy	pLimit
	bne	new_1
	lda	defs,x+
	mvy	defs,x+	y
	stx	defsIndex
	jmp	set

case_4
	lda	mulLookups+$1200,x
	adc	mulLookups+$1300,y
	sta	y
	lda	mulLookups+$1000,x
	add	mulLookups+$1100,y
	jmp	set
case_3
	lda	mulLookups+$e00,x
	adc	mulLookups+$f00,y
	sta	y
	lda	mulLookups+$c00,x
	add	mulLookups+$d00,y
	jmp	set
case_2
	lda	mulLookups+$a00,x
	adc	mulLookups+$b00,y
	sta	y
	lda	mulLookups+$800,x
	add	mulLookups+$900,y
	jmp	set
case_1
	lda	mulLookups+$600,x
	adc	mulLookups+$700,y
	sta	y
	lda	mulLookups+$400,x
	add	mulLookups+$500,y
	jmp	set
case_0
	lda	mulLookups+$200,x
	adc	mulLookups+$300,y
	sta	y
	lda	mulLookups,x
	add	mulLookups+$100,y
set
	tax
	ldy	^2a
	mva	branchTable,y	branch-1
	ldy	y
	ift	TEST_OVERFLOW
	cpy #192
	bcs error
	eif
 
	mva	lineHi,y	ptr+1
	lda	lineLo,y
	eor	col,x
	tay
	lda	mask,x
	ora:sta	(ptr),y
	ldy	y
	clc:bcc	*
branch
	ift	TEST_OVERFLOW
error
	lsr:rne ^1a
	eif

defs
; crystal 4
	dta	$72,$0a
	dta	25,%0000
	dta	65,0,100
	dta	0,65,3
	dta	25+25,%0000
	dta	65,0,30
	dta	0,65,125
	dta	25+25+25,%0000
	dta	65,0,170
	dta	0,65,125
	dta	0,%0100
	dta	94,164,14
	dta	-164-1,94,156
	dta	135,6
; Sierpinski Pentagon
	dta	$44,$0c
	dta	51,%0000
	dta	97,0,79
	dta	0,97,3
	dta	51+51,%0000
	dta	97,0,142
	dta	0,97,48
	dta	51+51+51,%0000
	dta	97,0,18
	dta	0,97,48
	dta	51+51+51+51,%0000
	dta	97,0,41
	dta	0,97,119
	dta	0,%0000
	dta	97,0,118
	dta	0,97,119
	dta	129,5
; Barnsley Fern
	dta	$b2,$0c
	dta	186,%0010	; p, %dcba signs
	dta	217,-9-1,40	; a,b,e
	dta	9,217,-5-1	; c,d,f
	dta	186+33,%0100
	dta	50,57,6
	dta	-57-1,50,129
	dta	186+33+33,%1110
	dta	63,-70-1,85
	dta	-64-1,-40-1,215
	dta	0,%0110
	dta	14,-19-1,45
	dta	-19-1,26,170
	dta	114,47 ; invariant point x,y
; Tree
	dta	$e8,$04
	dta	76,%0100
	dta	49,124,14
	dta	-88,110,61
	dta	76+76,%0010
	dta	118,-100,151
	dta	64,92,-3
	dta	76+76+12,%1101
	dta	-14,17,140
	dta	-115,-28,208
	dta	76+76+12+12,%1011
	dta	-8,-17,143
	dta	120,-5,82
	dta	0,%0001
	dta	-163,0,223
	dta	0,128,44
	dta	63,74
; Levy Dragon
	dta	$24,$08
	dta	128,%0010
	dta	128,-128-1,64
	dta	128,128,0
	dta	0,%0100
	dta	128,128,64
	dta	-128-1,128,127
	dta	128,188

	dta	0

	run	main
